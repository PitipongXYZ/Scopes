def VERSION = '0.1.2'

buildscript {
    repositories {
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:1.0.0'
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:0.5'
    }
}
apply plugin: 'java'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'maven-publish'

sourceSets {
    main {
        java {
            srcDirs =  ['src/main/java', '../scopesapi/src/main/java']
        }
    }
}

dependencies {
    compile project(':scopesapi')
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from 'build/docs/javadoc'
}

publishing {
    publications {
        dist(MavenPublication) {
            groupId 'me.emmano'
            version VERSION
            artifactId 'me.emmano.scopes.app.scopes-compiler'
            artifact "build/libs/scopescompiler.jar"
            artifact sourcesJar {
                classifier 'source'
            }
            artifact javadocJar {
                classifier 'javadoc'
            }
            pom.withXml {
                def root = asNode()
                def dependencies = root.appendNode('dependencies')
                configurations.compile.allDependencies.each {
                    if (it['group'] != null) {
                        def dependency = dependencies.appendNode('dependency')
                        dependency?.appendNode('groupId', it['group'])
                        dependency?.appendNode('artifactId', it['name'])
                        dependency?.appendNode('version', it['version'])
                    }
                }
            }
        }
    }
}

bintray {
    user = bintrayUser
    key = bintrayApiKey
    publications = ['dist']

    dryRun = false
    publish = false
    pkg {
        repo = 'emmano'
        name = 'me.emmano.me.emmano.scopes.app.scopes'
        desc = 'Scopes compiler'
        websiteUrl = 'https://github.com/emmano/Scopes/'
        issueTrackerUrl = 'https://github.com/emmano/Scopes/issues'
        vcsUrl = 'https://github.com/emmano/Scopes.git'
        licenses = ['MIT']
        labels = ['Android', 'Dagger', 'Retrofit']
        publicDownloadNumbers = true

        version {
            name = VERSION //Bintray logical VERSION name
            desc = 'Scopes compiler'
            vcsTag = VERSION
        }
    }
}